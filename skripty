dim_datumy = 
ADDCOLUMNS(    
SELECTCOLUMNS(
    CALENDARAUTO(),"datum",[Date]),
"rok",YEAR([datum]),
"kvartal",QUARTER([datum]),
"mesic",MONTH([datum]),
"tyden",WEEKNUM([datum],21))

________________________________________________________

dim_datumy = 
ADDCOLUMNS(
-- prejemnovavam sloupec vraceny funkci CALENDARAUTO()
SELECTCOLUMNS(
    CALENDARAUTO(),"datum",[Date]),
"rok",YEAR([datum]),
"kvartal",QUARTER([datum]),
"mesic",MONTH([datum]),
"tyden",WEEKNUM([datum],21),
"den",DAY([datum]),
"svatek",
SWITCH(TRUE(),
DAY([datum]) = 8 && MONTH([datum]) = 5,"svatek",
DAY([datum]) = 8 && MONTH([datum]) = 5,"svatek",
[datum] in {DATE(2020,04,12)},"svatek","nesvatek"),
"vikend",IF(WEEKDAY([datum],2)>5,"vikend","nevikend"))

________________________________________________

dim_typ_pocasi = 
UNION(
ROW("typ_pocasi","prselo"),
ROW("typ_pocasi","neprselo")
)
_________________________________________

01 suma srazek rok 2000 = 
-- TAKTO NE!
CALCULATE(SUM(data[Srazky]),FILTER(ALL(data),data[rok] = 2000
))

________________________________________________________________

02 suma srazek rok 2000 = 

-- TAKTO ANO, POKUD MI NEJDE VERZE 3
CALCULATE(SUM(data[Srazky]),FILTER(ALL(data[rok]),data[rok] = 2000))

______________________________

03 suma srazek rok 2000 = 
-- TAKTO ANO
CALCULATE(SUM(data[Srazky]),data[rok] = 2000)

__________________________________

09 maximalni rocni srazky = 
MAXX(
    ALL(data[rok]),
    CALCULATE(SUM(data[Srazky])))
    
____________________________________


10 minimalni maximalni teplota = 

-- minimum z tabulky maximalnich teplot
IF(MAX(data[Teplota]) = 
    MINX(
    ALL(data[rok]),
    CALCULATE(MAX(data[teplota]))),"CYAN")
_____________________________________________

11 suma srazek ruzyne = 
CALCULATE(SUM(data[Srazky]),
dim_lokality[Lokalita] = "RUZYNE")

___________________________________________

12 pocet dnu bez srazek v ruzyne = 
if(ISFILTERED(dim_datumy[rok]),
CALCULATE(
    COUNT(data[Datum]),
dim_lokality[Lokalita] = "RUZYNE",
data[Srazky] = 0,
NOT ISBLANK(data[Srazky])),
BLANK())

______________________________________________________

13 suma srazek predchozi rok = 
-- suma srazek ve stejnem mesici predchoziho roku
CALCULATE(
SUM(data[Srazky]),
dim_datumy[rok] = SELECTEDVALUE(dim_datumy[rok]) - 1)

_______________________________________________________________

14 suma srazek kumulativne = 
CALCULATE(
    SUM(data[Srazky]),
dim_datumy[mesic] <= SELECTEDVALUE(dim_datumy[mesic]))

_______________________________________________________________

15 prvni datum maximalni teploty = 
CALCULATE(
    MIN(data[Datum]),
data[Teplota] = MAX(data[Teplota]))

_________________________________________________________________

16 pocet dnu v ruzyni teplota pod 0 = 
CALCULATE(
    MAX(
    COUNT(data[Datum]),0),
dim_lokality[Lokalita] = "RUZYNE",
data[Teplota] < 0)
____________________________________________________________

17 suma srazek mezirocne = 

-- suma srazek dany rok a mesic
VAR srazky_dany_rok = SUM(data[Srazky])
-- suma srazek predchozi rok a dany mesic
VAR predchozi_rok = SELECTEDVALUE(dim_datumy[rok]) - 1
VAR srazky_predchozi_rok = 
CALCULATE(SUM(data[Srazky]),dim_datumy[rok] = predchozi_rok)

RETURN srazky_dany_rok - srazky_predchozi_rok

_______________________________________________________________

18 datumy maximalnich teplot ruzyne = 
-- v danem roce

VAR maximalni_teplota_ruzyne = 
CALCULATE(MAX(data[Teplota]),dim_lokality[Lokalita] = "RUZYNE")

-- tabulku datumu s maximalni teplotou v ruzyni


VAR tabulka_datumu = 

CALCULATETABLE(VALUES(data[Datum])
,data[Teplota] = maximalni_teplota_ruzyne
,dim_lokality[Lokalita] = "RUZYNE")

RETURN 
CONCATENATEX(tabulka_datumu,data[Datum],", ")

__________________________________________________________

19 tri nejvyssi teploty = 
VAR teplota_rank  =
FILTER(
ADDCOLUMNS(
   VALUES(data[Teplota]),
    "rank_teploty",RANKX(VALUES(data[Teplota]),data[Teplota],,DESC)),
    [rank_teploty] <=3)

return CONCATENATEX(teplota_rank,data[Teplota],", ",data[Teplota],DESC)

__________________________________________________

20 rok max a min srazek = 

VAR srazky_rok_mesic = ROUND(SUM(data[Srazky]),2)
VAR srazky = 
ADDCOLUMNS(
ALL(data[rok]),
"mesicni srazky",
CALCULATE(SUM(data[Srazky])))
VAR min_srazky = ROUND(MINX(srazky,[mesicni srazky]),2)
VAR max_srazky = ROUND(MAXX(srazky,[mesicni srazky]),2)

RETURN
SWITCH(TRUE(),
srazky_rok_mesic = min_srazky,"GOLD",
srazky_rok_mesic = max_srazky,"CYAN")

______________________________________________________

21 roky maximalnich srazek = 
-- pro danou lokalitu 
-- 1. Tabulku roku respektujici filtr nad lokalitou
-- 2. Do tabulky dopocitame rocni srazky (novy sloupec)
-- 3. V tabulce z bodu 2 hledame maximalni hodnotu
-- 4. Spojujeme sloupec rok
VAR tabulka_rocni_srazky = 
ADDCOLUMNS(
    VALUES(data[rok]),
    "rocni_srazky",
    CALCULATE(SUM(data[Srazky])))
VAR maximalni_rocni_srazky = MAXX(tabulka_rocni_srazky,[rocni_srazky])
VAR tabulka_max_rocnich_srazek = 
FILTER(tabulka_rocni_srazky,[rocni_srazky] =maximalni_rocni_srazky )
RETURN CONCATENATEX(tabulka_max_rocnich_srazek,data[rok]," ,")


_____________________________________________________________

22 nejdestivejsi mesic v lokalite = 

-- 1. Tabulku mesicu
-- 2. Novy sloupecek se sumou srazek pro dany mesic
-- 3. Maximum z tabulky z bodu 2
-- 4. Hodnoty z tabulky filtrovane na hodnotu z bodu 3

VAR tabulka_mesicni_srazky = 
ADDCOLUMNS(
    VALUES(data[mesic]),
"mesicni_srazky",
CALCULATE(SUM(data[Srazky])))

VAR maximalni_mesicni_srazky = MAXX(tabulka_mesicni_srazky,[mesicni_srazky])

VAR tabulka_max_srazek  =
FILTER(tabulka_mesicni_srazky,[mesicni_srazky] = maximalni_mesicni_srazky)

RETURN
CONCATENATEX(tabulka_max_srazek,data[mesic]," ,")

________________________________________

23 nejdestivejsi lokalita = 
-- Pokud se suma srazek v dane lokalite rovna maximalni sume srazek
-- dle lokality napric lokalitami, tak "ano", jinak "ne"
-- 1. suma srazek v dane lokalite
-- 2. tabulku se vsemi lokalitami se sumou srazek dle lokality
-- 3. maximum z bodu 2
-- 4. Jestli se 1 a 3 rovnaji, pokud ano, tak "ano", jinak "ne"
VAR suma_srazek = SUM(data[Srazky])
VAR tabulka_srazek =
    ADDCOLUMNS(
    ALL(data[Lokalita]),
    "suma srazek",
    CALCULATE(SUM(data[Srazky])))

VAR max_srazky = MAXX(tabulka_srazek,[suma srazek])

RETURN
IF(suma_srazek = max_srazky,"ANO","NE")

___________________________________________________________

27 nejteplejsi lokality = 

VAR max_teplota = MAX(data[Teplota])
VAR lokality_max_teploty = 
CALCULATETABLE(VALUES(data[Lokalita]),data[Teplota] = max_teplota)

RETURN
CONCATENATEX(lokality_max_teploty,data[Lokalita],", ")


______________________________________________________________

28 dny max teploty = 
VAR tabulka_max_teplot = 
CALCULATETABLE(VALUES(data[Datum]),data[Teplota] = MAX(data[Teplota]))
RETURN 
CONCATENATEX(tabulka_max_teplot,data[Datum],", ")
__________________________________________________________

29 suma srazek predchozi mesic = 

VAR konec_predchozi_mesic = EOMONTH(MAX(dim_datumy[datum]),-1)
VAR konec_predpredchozi_mesic = EOMONTH(MAX(dim_datumy[datum]),-2)

RETURN
CALCULATE(
    SUM(data[Srazky])
    ,ALL(dim_datumy[rok],dim_datumy[mesic])
    ,dim_datumy[datum] > konec_predpredchozi_mesic
    && dim_datumy[datum] <= konec_predchozi_mesic) 

__________________________________________________________________

30 maximalni teplota mesice a roku = 
VAR mesic = SELECTEDVALUE(dim_datumy[mesic])
VAR rok = SELECTEDVALUE(dim_datumy[rok])

-- maximalni teplota daneho dne
VAR max_teplota = MAX(data[Teplota])
-- maximalni teplota daneho mesic
VAR max_teplota_mesic = 
CALCULATE(MAX(data[Teplota]),
ALL(dim_datumy[datum]),
dim_datumy[rok] = rok,
dim_datumy[mesic] = mesic)

RETURN 
IF(max_teplota = max_teplota_mesic,"tomato")

_________________________________________________________________

chybejici datumy = 
-- sumarizujeme / groupujeme / agregujeme tabulku tak,
-- aby v ni nebyly vsechny datumy vypsany, ale agregovany,
 -- na vystupu bude pocet dnu chybejiciho zaznamu pro danou lokalitu
SUMMARIZE(
    FILTER(
    -- pridany sloupec zaznam
    ADDCOLUMNS(
    -- tabulka se vsemi datumy a lokalitami
    GENERATE(
        ALL(dim_datumy[datum]),
        ALL(data[Lokalita])
    ),"zaznam",
    MAX(
    CALCULATE(COUNT(data[Datum])),0)),
    [zaznam] = 0),
    data[Lokalita],"pocet chybejicich",COUNT(dim_datumy[datum]))
________________________________________________________________________

35 suma srazek roky = 
CALCULATE(
    SUM(data[Srazky]),
    USERELATIONSHIP(data[rok],dim_roky[rok]))
